apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../dependencies.gradle'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
final def keystorePropertiesFile = rootProject.file("keystore.properties")

final def gitHash = { ->
    if (System.getenv("WITH_MAINFRAMER") == "true" ||
            System.getenv("TRAVIS") == "true") {
        "TEST"
    } else {
        final def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        stdout.toString().trim()
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion versions.compileSdk
    dataBinding.enabled true
    android.applicationVariants.all { final variant ->
        variant.outputs.all {
            outputFileName = "wg-android_${gitHash()}.apk"
        }
    }
    defaultConfig {
        applicationId 'me.msfjarvis.wgandroid'
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode versions.publishVersionCode
        versionName versions.publishVersion
        buildConfigField 'int', 'MIN_SDK_VERSION', "$minSdkVersion.apiLevel"
    }
    // If the keystore file exists
    if (keystorePropertiesFile.exists()) {
        // Initialize a new Properties() object called keystoreProperties.
        final def keystoreProperties = new Properties()

        // Load your keystore.properties file into the keystoreProperties object.
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) signingConfig signingConfigs.release
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${android.defaultConfig.applicationId}"
                }
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${android.defaultConfig.applicationId}${applicationIdSuffix}"
                }
            }
            minifyEnabled false
        }
    }
    externalNativeBuild {
        cmake {
            path 'tools/CMakeLists.txt'
        }
    }
    lintOptions.abortOnError false
}

dependencies {
    implementation "androidx.annotation:annotation:$versions.androidx"
    implementation "androidx.appcompat:appcompat:$versions.androidx"
    implementation "androidx.cardview:cardview:$versions.androidx"
    implementation "androidx.core:core-ktx:$versions.androidx"
    implementation "androidx.databinding:databinding-adapters:$versions.databinding"
    implementation "androidx.databinding:databinding-runtime:$versions.databinding"
    implementation "androidx.fragment:fragment-ktx:$versions.androidx"
    implementation "androidx.preference:preference:$versions.androidx"
    implementation "com.google.android.material:material:$versions.materialcomponents"
    implementation "com.jakewharton.timber:timber:$versions.timber"
    implementation "com.journeyapps:zxing-android-embedded:$versions.zxing"
    implementation "net.sourceforge.streamsupport:android-retrofuture:$versions.streamsupport"
    implementation "net.sourceforge.streamsupport:android-retrostreams:$versions.streamsupport"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}
repositories {
    mavenCentral()
}
